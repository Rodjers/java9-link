apply plugin: 'java-library'

final jdk9Home = "/usr/lib/jvm/jdk-9"
final mainClassName = 'example.server.Runner'
sourceCompatibility = project.hasProperty('javaVersion') ? project.javaVersion : JavaVersion.VERSION_1_9


repositories {
    jcenter()
}

dependencies {
}

task customJre(dependsOn: jar) {
    def outputPath = "${buildDir}/custom-jre"
    ext.output = file(outputPath)

    inputs.file jar.archivePath
    outputs.dir outputPath

    doLast {
        delete output
        exec {
            executable "${jdk9Home}/bin/jlink"
            args = ['--module-path', "$jdk9Home/jmods:${jar.archivePath}", '--add-modules', 'example.server',
                    '--output', outputPath,
                    '--compress=2', '--strip-debug'
            ]
        }
        // fix non-writable files in legal/
        exec {
            executable 'chmod'
            args = ['u+w', '-R', outputPath]
        }
    }
}
build.dependsOn customJre

if (sourceCompatibility == JavaVersion.VERSION_1_9) {
    tasks.withType(JavaCompile) {
        options.fork = true
        options.forkOptions.executable = "${jdk9Home}/bin/javac"
    }
} else {
    sourceSets.main.java.exclude("**/module-info.java")
    customJre.enabled = false
}

jar {
    manifest {
        attributes('Main-Class': mainClassName)
    }
}

clean {
    delete 'build/custom-jre'
    delete 'build/docker'
}

task copyFilesForDocker(type: Copy, dependsOn: [customJre, jar]) {
    from file('Dockerfile')
    from file('Dockerfile-9')
    from(tasks.jar)
    from(customJre.output) {
        into "custom-jre"
    }
    into 'build/docker'
}

task image(dependsOn: copyFilesForDocker) {
    def imageName = "${project.name}:jre${sourceCompatibility.majorVersion}"
    group = 'Build'
    description = "Build Docker image $imageName"
    doLast {
        exec {
            executable 'docker'
            args = ['build', '-t', imageName]
            if (sourceCompatibility == JavaVersion.VERSION_1_9) {
                args += ['--file', 'Dockerfile-9']
            }
            args += ['.']
            workingDir 'build/docker'
        }
        println "Built Docker image $imageName"
    }
}
