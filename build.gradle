plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.4' apply false
}

final jdk9Home = "/usr/lib/jvm/jdk-9"
final javaVersion = project.hasProperty('javaVersion') ? JavaVersion.toVersion(project.javaVersion)
        : JavaVersion.VERSION_1_9
final customJreOutput = file("${buildDir}/custom-jre")

subprojects {
    apply plugin: 'java'
    if (project.name == 'greeter-server') {
        apply plugin: 'com.github.johnrengelman.shadow'
    }

    sourceCompatibility = javaVersion

    repositories {
        jcenter()
    }

    dependencies {
    }
}

task clean(type: Delete) {
    delete 'build/custom-jre'
    delete 'build/docker'
}

task build

if (javaVersion == JavaVersion.VERSION_1_9) {
    def dependenciesDir = rootProject.file('build/dependencies')
    subprojects {
        task copyDependenciesAsModules(type: Sync) {
            from configurations.runtime
            into dependenciesDir
        }

        tasks.withType(JavaCompile) {
            dependsOn copyDependenciesAsModules
            options.fork = true
            options.forkOptions.executable = "${jdk9Home}/bin/javac"
            options.compilerArgs = ['--module-path', [rootProject.file('greeter-protocol/build/libs'), dependenciesDir].join(':')]
            doLast {
                def compileOpts = file('build/tmp/compileJava/java-compiler-args.txt').text.replaceAll("\n", " ")
                logger.lifecycle "${options.forkOptions.executable} ${compileOpts}"
            }
        }
    }
    task customJre(dependsOn: project(':greeter-server').tasks.jar) {
        ext.appModulePaths = [dependenciesDir,
                              project(':greeter-server').libsDir]

        outputs.dir customJreOutput
        ext.appModulePaths.each { outputs.dir it }

        doLast {
            delete customJreOutput
            exec {
                executable "${jdk9Home}/bin/jlink"
                args = ['--module-path', (["$jdk9Home/jmods"] + ext.appModulePaths).grep().join(':'),
                        '--add-modules', 'example.greeter.server',
                        '--output', customJreOutput,
                        '--compress=2', '--strip-debug'
                ]
                logger.lifecycle commandLine.join(' ')
            }
            // fix non-writable files in legal/
            exec {
                executable 'chmod'
                args = ['u+w', '-R', customJreOutput]
            }
        }
    }
    build.dependsOn customJre

    task copyFilesForDocker(type: Sync, dependsOn: [customJre]) {
        from file('Dockerfile-9')
        from(customJreOutput) {
            into "custom-jre"
        }
        into 'build/docker'
    }
} else {
    subprojects {
        sourceSets.main.java.exclude("**/module-info.java")
    }
    def shadowJarTask = project(':greeter-server').tasks.shadowJar
    build.dependsOn shadowJarTask

    task copyFilesForDocker(type: Sync, dependsOn: [shadowJarTask]) {
        from file('Dockerfile')
        from(shadowJarTask)
        into 'build/docker'
    }
}


task image(dependsOn: copyFilesForDocker) {
    def imageName = "${project.name}:jre${javaVersion.majorVersion}"
    group = 'Build'
    description = "Build Docker image $imageName"
    doLast {
        exec {
            executable 'docker'
            args = ['build', '-t', imageName]
//            args += ['--pull']
            if (javaVersion == JavaVersion.VERSION_1_9) {
                args += ['--file', 'Dockerfile-9']
            }
            args += ['.']
            workingDir 'build/docker'
        }
        println "Built Docker image $imageName"
    }
}
